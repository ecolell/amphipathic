' CREA LOS ARCHIVOS
10 ON ERROR GOTO 580
20 CLS:DIM HID$(2,20)
30 LOCATE 3,5:PRINT "Camino de acceso y nombre del archivo"
40 LOCATE 4,5:INPUT "con las secuencias pept¡dicas [.PEP]: ",INSEQ$
50 IF INSTR(INSEQ$,".")=0 THEN INSEQ$=INSEQ$+".PEP"
60 FL1%=1:OPEN INSEQ$ FOR INPUT AS #1:CLOSE #1:FL1%=0
70 CLS
80 LOCATE 3,5:PRINT "Camino de acceso y nombre del archivo"
90 LOCATE 4,5:INPUT "con la tabla de datos de hidrofobicidad [.TAB]: ",TABENE$
100 IF INSTR(TABENE$,".")=0 THEN TABENE$=TABENE$+".TAB"
110 FL2%=1:OPEN TABENE$ FOR INPUT AS #1:CLOSE #1:FL2%=0
120 CLS
130 LOCATE 3,5:PRINT "Camino de acceso y nombre del archivo"
140 LOCATE 4,5:INPUT "de salida [.HID]: ",OUTENE$
150 IF INSTR(OUTENE$,".")=0 THEN OUTENE$=OUTENE$+".HID"
160 FL3%=1:OPEN OUTENE$ FOR INPUT AS #3:CLOSE #3
170 FL3%=0:LOCATE 7,15:PRINT "EL ARCHIVO EXISTE!!!!"
180 LOCATE 9,15:PRINT "Cambia el nombre [C]"
190 LOCATE 10,15:PRINT "Sobreescribe     [S]"
200 LOCATE 11,15:PRINT "Agrega datos     [A]"
210 A$=INKEY$:IF A$="" THEN 210
220 IF A$="C" OR A$="c" THEN CLS:OUTENE$="":GOTO 130
230 IF A$="A" OR A$="a" THEN OPEN OUTENE$ FOR APPEND AS #3:CLS:GOTO 252
240 IF A$<>"S" AND A$<>"s" THEN 210
250 CLS:OPEN OUTENE$ FOR OUTPUT AS #3
252 IF INSTR(OUTENE$,".")=0 then outfile1$=outfile1$+".AI":goto 258
253 for m1=1 to len(outene$)
254 if mid$(outene$,m1,1)="." then po1%=m1:goto 256
255 next m1
256 outfile1$=mid$(outene$,1,po1%-1)+".AI"
258 OPEN OUTFILE1$ FOR OUTPUT AS #4
260 OPEN INSEQ$ FOR INPUT AS #1
270 OPEN "R",#2,TABENE$,6
280 FIELD #2,1 AS AMI$,5 AS HIDROF$
' PREGUNTA POR LOS ANGULOS
290 CLS:LOCATE 7,10:INPUT "Angulo inicial del area a integrar (expresado en grados): ",ANGINI%
300 LOCATE 9,10:INPUT "Angulo final del area a integrar (expresado en grados): ",ANGFIN%
310 LOCATE 11,10:INPUT "Frecuencia de analisis (expresada en grados): ",FREC
320 FGR=.0174533
330 FOR J=1 TO 20
340 GET #2,J
350 HID$(1,J-1)=AMI$:HID$(2,J-1)=HIDROF$
360 NEXT J
370 CLOSE #2
380 IF EOF(1) THEN 530
390 LINE INPUT #1, SEQ$:LONG%=LEN(SEQ$)
400 POSI%=INSTR(SEQ$,"#"):IF POSI%=0 THEN PEP$=SEQ$:GOTO 440
410 NOM$=MID$(SEQ$,1,POSI%-1)
420 PEP$=MID$(SEQ$,POSI%+2,LONG%-POSI%+2)
430 GOSUB 1500
440 GOSUB 660
450 PRINT #3,"":PRINT #3,NOM$;PEP$,"Length: ";LEN(PEP$);" aa."
455 PRINT #4,"":PRINT #4,NOM$;PEP$,"Length: ";LEN(PEP$);" aa."
460 PRINT #3,"":PRINT #3,USING "& ###.#####      & ###.####";"Amphipathic Index:";AI;"Mean hydrophobicity: ";HM
465 PRINT #4,"":PRINT #4,USING "& ###.#####      & ###.####";"Amphipathic Index:";AI;"Mean hydrophobicity: ";HM
468 PRINT #4,""
470 PRINT #3,"":PRINT #3,"Power Spectrum Data"
480 PRINT #3,"":PRINT #3," Frequency      Period         Power        Norm. Power    Norm. Power"
490 PRINT #3,string$(29,chr$(32));"(100";chr$(248);"=1000)  (Max.Power=1000)"
500 GOSUB 1200
510 PRINT #3,"":POWER=0:SEQ$="":NOM$="":PEP$=""
520 GOTO 380
530 CLOSE:CLS:LOCATE 10,18:PRINT STRING$(43,"*")
540 LOCATE 11,18:PRINT "*";STRING$(41,CHR$(32));"*"
550 LOCATE 12,18:PRINT "*               C'est fini                *"
560 LOCATE 13,18:PRINT "*";STRING$(41,CHR$(32));"*"
570 LOCATE 14,18:PRINT STRING$(43,"*"):END
580 IF ERR=53 AND FL3%=1 THEN FL3%=0:RESUME 250
590 IF ERR=53 THEN CLS:LOCATE 10,25:PRINT "El file no existe!!!":GOTO 620
600 IF ERR=76 THEN CLS:LOCATE 10,25:PRINT "Error en el camino de acceso!!!":GOTO 620
610 PRINT "ERROR LINE: ";ERL,"ERROR: ";ERR:END
620 LOCATE 20,35:PRINT "Apriete cualquier tecla para continuar"
630 A$=INKEY$:IF A$="" THEN 630
640 IF FL1%=1 THEN FL1%=0:INSEQ$="":CLS:RESUME 30
650 IF FL2%=1 THEN FL2%=0:TABENE$="":CLS:RESUME 80
660 CLS:LOCATE 10,18:PRINT STRING$(43,"*")
670 LOCATE 11,18:PRINT "*";STRING$(41,CHR$(32));"*"
680 LOCATE 12,18:PRINT "*  Calculando el indice de anfipaticidad  *"
690 LOCATE 13,18:PRINT "*";STRING$(41,CHR$(32));"*"
700 LOCATE 14,18:PRINT  STRING$(43,"*")
710 A%=ANGINI%
720 B%=ANGFIN%
730 NA%=B%-A%
740 PAS=FREC
750 GOSUB 870
760 NUM=M1*(F1*SUMTOT)
770 SUMPARC1=0:SUMPARC2=0:SUMPARC3=0:SUMTOT=0
780 A%=1
790 B%=180
800 NA%=180
810 PAS=FREC
820 GOSUB 870
830 DEN=M1*(F1*SUMTOT)
840 SUMPARC1=0:SUMPARC2=0:SUMPARC3=0:SUMTOT=0
850 AI=NUM/DEN
860 RETURN
870 M1=1/(NA%*FGR)
880 F1=((B%*FGR-A%*FGR)/NA%*FGR)/2
890 FOR K1=1 TO LEN(PEP$)
900 FOR KK1=0 TO 19
910 IF MID$(PEP$,K1,1)=HID$(1,KK1) THEN H=VAL(HID$(2,KK1)):GOTO 930
920 NEXT KK1
930 SUM1=SUM1+((H-HM)*COS((K1-1)*A%*FGR))
940 SUM2=SUM2+((H-HM)*SIN((K1-1)*A%*FGR))
950 NEXT K1
960 SUMPARC1=(SUM1^2)+(SUM2^2)
970 SUM1=0:SUM2=0
980 FOR J1=A%+PAS TO B%-PAS STEP PAS
990 FOR K2=1 TO LEN(PEP$)
1000 FOR KK2=0 TO 19
1010 IF MID$(PEP$,K2,1)=HID$(1,KK2) THEN H=VAL(HID$(2,KK2)):GOTO 1030
1020 NEXT KK2
1030 SUM1=SUM1+((H-HM)*COS((K2-1)*J1*FGR))
1040 SUM2=SUM2+((H-HM)*SIN((K2-1)*J1*FGR))
1050 NEXT K2
1060 SUMPARC2=SUMPARC2+2*((SUM1^2)+(SUM2^2))
1070 SUM1=0:SUM2=0
1080 NEXT J1
1090 FOR K3=1 TO LEN(PEP$)
1100 FOR KK3=0 TO 19
1110 IF MID$(PEP$,K3,1)=HID$(1,KK3) THEN H=VAL(HID$(2,KK3)):GOTO 1130
1120 NEXT KK3
1130 SUM1=SUM1+((H-HM)*COS((K3-1)*B%*FGR))
1140 SUM2=SUM2+((H-HM)*SIN((K3-1)*B%*FGR))
1150 NEXT K3
1160 SUMPARC3=(SUM1^2)+(SUM2^2)
1170 SUM1=0:SUM2=0
1180 SUMTOT=SUMPARC1+SUMPARC2+SUMPARC3
1190 RETURN
1200 CLS:LOCATE 10,18:PRINT STRING$(43,"*")
1210 LOCATE 11,18:PRINT "*";STRING$(41, CHR$(32));"*"
1220 LOCATE 12,18:PRINT "*       Calculando el Power Spectrum      *"
1230 LOCATE 13,18:PRINT "*";STRING$(41,CHR$(32));"*"
1240 LOCATE 14,18:PRINT STRING$(43,"*")
1250 OPEN "ark.tmp" FOR OUTPUT AS #2
1260 POWERMAX=0:POWER100=0
1270 FOR J2=A% TO B% STEP PAS
1280 FOR K5=1 TO LEN(PEP$)
1290 FOR KK5=0 TO 19
1300 IF MID$(PEP$,K5,1)=HID$(1,KK5) THEN H=VAL(HID$(2,KK5)):GOTO 1320
1310 NEXT KK5
1320 SUM1=SUM1+((H-HM)*COS((K5-1)*J2*FGR))
1330 SUM2=SUM2+((H-HM)*SIN((K5-1)*J2*FGR))
1340 NEXT K5
1350 POWER=(SUM1^2)+(SUM2^2)
1360 IF J2=100 THEN POWER100=POWER
1370 IF POWER>POWERMAX THEN POWERMAX=POWER
1380 SUM1=0:SUM2=0
1390 PRINT #2, USING "  ###.##    #####.####";J2;POWER
1400 NEXT J2
1410 CLOSE #2:NORMF1=1000/POWER100:NORMF2=1000/POWERMAX
1420 OPEN "ark.tmp" FOR INPUT AS #2
1430 FOR J3=A% TO B% STEP PAS
1440 LINE INPUT #2,DAT$
1450 POWER=VAL(MID$(DAT$,12,11))
1455 PERIOD=(2*3.14159265358979)/((3.14159265358979/180)*J3)
1460 PRINT #3, USING "  ###.##    #####.####      #####.####      #####.####     #####.####";J3;PERIOD;POWER;(POWER*NORMF1);(POWER*NORMF2)
1470 NEXT J3
1480 CLOSE #2:PERIOD=0:POWER=0:NORMF1=0:NORMF2=0
1490 RETURN
1500 CLS:LOCATE 10,18:PRINT STRING$(43,"*")
1510 LOCATE 11,18:PRINT "*";STRING$(41, CHR$(32));"*"
1520 LOCATE 12,18:PRINT "*   Calculando la hidrofobicidad media    *"
1530 LOCATE 13,18:PRINT "*";STRING$(41,CHR$(32));"*"
1540 LOCATE 14,18:PRINT STRING$(43,"*")
1550 FOR K=1 TO LEN(PEP$)
1560 FOR KK=0 TO 19
1570 IF MID$(PEP$,K,1)=HID$(1,KK) THEN SUMHIDR=SUMHIDR+VAL(HID$(2,KK)):GOTO 1590
1580 NEXT KK
1590 NEXT K
1600 HM=SUMHIDR/LEN(PEP$):SUMHIDR=0:RETURN

